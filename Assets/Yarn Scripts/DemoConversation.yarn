title: Start
---
Press Tab to open the debug window and load some scripts, or press the Continue button for an example of a conversation between 2 characters.
<<jump DemoConversation>>
===
title: DemoConversation
tags:
---
<<bg "LanaBedroom">>
<<bg_color_rgba 0.0 0.0 0.0>>
<<char_add "BerylVegha" "BerylVegha" Center>>
<<char_add "LanaElenore" "LanaElenore" OffscreenRight>>
<<char_move_obj_time BerylVegha Center>>
<<char_move_obj_time LanaElenore OffscreenRight>>
<<bg_color_rgba 1.0 1.0 1.0 1.0 1.0 waitForAnimation>>
<<char_show BerylVegha 0.2>>
Beryl: Hey! My name's Beryl!
Beryl: If I'm appearing on your screen right now, that means everything is working!
Beryl: Real quick I want to try a memory game. Guess any number you want, as long as it's between 1 and 5.
->1
    <<set $chosenNumber = 1>>
    Beryl: 1 huh? Pretty basic if you ask me.
->2
    <<set $chosenNumber = 2>>
    Beryl: 2 is just as bad as 1.
->3
    <<set $chosenNumber = 3>>
    Beryl: The magic number 3. Alright!
->4
    <<set $chosenNumber = 4>>
    Beryl: 4's pretty okay. I guess.
->5
    <<set $chosenNumber = 5>>
    Beryl: 5. Got it.
Beryl: Try to remember the number you chose, cuz I'm gonna ask you about it again later!
Beryl: Alright, now I'm going to try disappearing.
<<char_hide BerylVegha 0.2 false 0.0 0.0 0.0>>
Beryl: Can you see me? No? That means it worked!
<<char_show BerylVegha 0.2 false 0.0 0.0 0.0>>
Beryl: Alright, now I'm gonna move around.
Beryl: First to the far left...
<<char_move BerylVegha 0 1000 smoothLerp>>
Beryl: Then the far right...
<<char_move BerylVegha 4 1000>>
Beryl: To the left now!
<<char_move BerylVegha 1 1000>>
Beryl: How about the right?
<<char_move BerylVegha 3 1000>>
Beryl: Finally, to the center of the stage! I'm gonna do it real slow this time so you can watch me as I go.
<<char_move_obj_time BerylVegha Center 3 smoothLerp>>
<<wait_anim>>
Beryl: That's just the way I like it.
Beryl: Real quick let's try out a cool effect. It's called "speed lines". Ready? Here I go!
<<vfx_char "speedline" BerylVegha 10.0>>
Beryl: Wow, look at those things!
Beryl: How about a little change of view? I'm a bit tired of this stuffy old room.
<<char_hide BerylVegha 0.2 false 0.0 0.0 0.0>>
<<bg_color_rgba 0.0 0.0 0.0 1.0 1.0>>
You and Beryl go for a walk down the hall...
<<bg "Barracks" 1.0>>
...and enter a large room full of weapons and armor.
<<bg_color_rgba 1.0 1.0 1.0 1.0 1.0 waitForAnimation>>
<<char_show BerylVegha 0.2 false 0.0 0.0 0.0>>
Beryl: A barracks? Hmm, not the most fun place to be but it'll do for now.
Beryl: Alright, how about something a little different? I'm going to try calling one of my friends, and we're going to try having a conversation. Does that sound good?
-> Sure
    Beryl: Great! Glad you agree.
    <<jump FriendlyConversation>>
-> Not really
    Beryl: Wrong answer. This is happening whether you like it or not.
    <<jump FriendlyConversation>>
===

title: FriendlyConversation
---
Beryl: Alright, are you ready? Lanaaaaaa! Hey Lana, come here for a second, I want to talk to you about something.
<<char_move_obj_time BerylVegha Left 0.2>>
<<char_show LanaElenore 0.2 false 0.0 0.0 0.0>>
<<char_move_obj_time LanaElenore Right 0.2>>
Lana comes into the room.
<<char_dim_alternate BerylVegha LanaElenore 0.2 false false undimSecondCharacter>>
Lana: Sup.
<<char_dim_alternate BerylVegha LanaElenore 0.2>>
Beryl: Hey look Lana, we're talking to each other right now! Isn't that cool?
<<char_dim_alternate BerylVegha LanaElenore 0.2>>
Lana: Sure, I guess. Communication is important.
<<char_dim_alternate BerylVegha LanaElenore 0.2>>
Beryl: It sure is!
Beryl: Okay Lana. Before you got here I was playing a little memory game with our friend here. I asked them to choose a number between 1 and 5. Let's see if they can remember what it was.
<<char_dim_alternate BerylVegha LanaElenore 0.2>>
Lana: That's what you called me in here for?
<<char_dim_alternate BerylVegha LanaElenore 0.2>>
Beryl: Okay, 1 through 5. Which number did you choose?
->1
    <<set $guessedNumber = 1>>
    <<char_dim_alternate BerylVegha LanaElenore 0.2>>
    Lana: 1? Seriously?
->2
    <<set $guessedNumber = 2>>
    <<char_dim_alternate BerylVegha LanaElenore 0.2>>
    Lana: Not what I would have chosen...
->3
    <<set $guessedNumber = 3>>
    <<char_dim_alternate BerylVegha LanaElenore 0.2>>
    Lana: 3 huh? Hm.
    Lana appears to be judging you.
->4
    <<set $guessedNumber = 4>>
    <<char_dim_alternate BerylVegha LanaElenore 0.2>>
    Lana: 4 is pretty okay I guess.
->5
    <<set $guessedNumber = 5>>
    <<char_dim_alternate BerylVegha LanaElenore 0.2>>
    Lana: Oh yeah, the biggest number. That's what I'm talking about.
<<if $chosenNumber == $guessedNumber>>
    <<char_dim_alternate BerylVegha LanaElenore 0.2>>
    Beryl: Yay! You guessed right! That must mean you have a pretty good memory.
    <<char_dim_alternate BerylVegha LanaElenore 0.2>>
    Lana: Ohhh wow that was so amazing.
<<else>>
    <<char_dim_alternate BerylVegha LanaElenore 0.2>>
    Beryl: Oh no! That's completely wrong! Better luck next time, I suppose.
    <<char_dim_alternate BerylVegha LanaElenore 0.2>>
    Lana: Oh well, at least you tried. You did try, didn't you?
<<endif>>
<<char_dim_alternate BerylVegha LanaElenore 0.2>>
Beryl: Alrighty then. That's about all I had to show off for today, so I think I'm gonna go and leave you to it. Bye!
<<char_hide BerylVegha 0.2 false 0.0 0.0 0.0>>
Beryl disappears.
<<char_undim LanaElenore 0.2>>
Lana: Uh...Well I guess I'll be going too. Bye.
<<char_hide LanaElenore 0.2 false 0.0 0.0 0.0>>
Lana quickly leaves.
<<bg_color_rgba 0.0 0.0 0.0 1.0 1.0 waitForAnimation>>
===